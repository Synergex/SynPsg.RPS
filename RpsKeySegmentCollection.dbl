;******************************************************************************
;
; Class:		SynPsg.Rps.RpsKeySegmentCollection
;
; Description:	Represents a collection of key segments for a Repository
;				structures key
;
; Author:		Steve Ives, Synergex Professional Services Group
;
; Date: 		19th October 2007
;
;******************************************************************************
;
; This code is supplied as seen and without warranty or support, and is used
; at your own risk. Neither the author or Synergex accept any responsability
; for any loss or damage which may result from the use of this code.
;
;******************************************************************************
;
; $Revision: 1 $
;
; $Date: 2010-04-13 17:06:56-07:00 $
;
;******************************************************************************
;
import SynPsg.Rps
.ifdef DBLV915
import Synergex.SynergyDE.Collections
.else
import System.Collections
.endc

namespace SynPsg.Rps

	public class RpsKeySegmentCollection
		
		private p_Collection, @ArrayList

		;Constructor (default capacity)
		public method RpsKeySegmentCollection
			required in KeyInfo, k_info
			endparams
		proc
			p_Collection = new ArrayList()
			LoadKeySegments(KeyInfo)
			mreturn
		endmethod
		
		;Constructor (specified capacity)
		public method RpsKeySegmentCollection
			required in KeyInfo, k_info
			required in Capacity, int
			endparams
		proc
			p_Collection = new ArrayList(Capacity)
			LoadKeySegments(KeyInfo)
			mreturn
		endmethod
		
		; Destructor
		method ~RpsKeySegmentCollection
		proc
			clear p_Collection
		endmethod

		;Load key segment information
		private method LoadKeySegments, void
			required in KeyInfo, k_info
			endparams
			record 
				ix, int
			endrecord
		proc
			for ix from 1 thru KeyInfo.ki_nmseg
			begin
				data seg, @RpsKeySegment
				seg = new RpsKeySegment(
				&	KeyInfo.ki_segtyp[ix],
				&	KeyInfo.ki_segpos[ix],
				&	KeyInfo.ki_seglen[ix],
				&	KeyInfo.ki_fldnam[ix],
				&	KeyInfo.ki_strnam[ix],
				&	KeyInfo.ki_litval[ix],
				&	KeyInfo.ki_segdtyp[ix],
				&	KeyInfo.ki_segord[ix])
				p_Collection.Add(seg)
			end

		endmethod
		
		;-----------------------------------------------------------------------
		;The follwoing members make the collection look just like an ArrayList
		
		;Gets or sets the number of elements that the collection can contain.
		public property Capacity, int
			method get
			proc
				mreturn p_Collection.Capacity
			endmethod
			method set
			proc
				p_Collection.Capacity = value
			endmethod
		endproperty

		;Gets the number of elements actually contained in the collection.
		public property Count, int
			method get
			proc
				mreturn p_Collection.Count
			endmethod
		endproperty

		;Gets or sets the element at the specified index.
		public property Indexer, @RpsKeySegment
			required in Index, int
			method get
			proc
				mreturn (RpsKeySegment)p_Collection[Index]
			endmethod
			method set
			proc
				p_Collection[Index] = value
			endmethod
		endproperty

		;Adds an element to the end of the collection. 
		public method Add, int
			required in RpsKeySegment, @RpsKeySegment
			endparams
		proc
			mreturn p_Collection.Add(RpsKeySegment)
		endmethod

		;Removes the element at the specified index of the collection.
		public method RemoveAt, void
			required in Index, int
		proc
			p_Collection.RemoveAt(Index)
		endmethod

		;Searches for the specified element and returns the one-based index
		;of the first occurrence within the entire collection.
		public method IndexOf, int
			required in RpsKeySegment, @RpsKeySegment
		proc
			mreturn p_Collection.IndexOf(RpsKeySegment)
		endmethod

		;Searches for the specified element and returns the one-based index
		;of the first occurrence within the range of elements in the collection
		;that extends from the specified index to the last element.
		public method IndexOf, int
			required in RpsKeySegment, @RpsKeySegment
			required in StartIndex, int
		proc
			mreturn p_Collection.IndexOf(RpsKeySegment,StartIndex)
		endmethod

		;Searches for the specified element and returns the one-based index
		;of the first occurrence within the range of elements in the collection
		;that starts at the specified index and contains the specified number of
		;elements. 
		public method IndexOf, int
			required in RpsKeySegment, @RpsKeySegment
			required in StartIndex, int
			required in Count, int
		proc
			mreturn p_Collection.IndexOf(RpsKeySegment,StartIndex,Count)
		endmethod

		;Removes all elements from the collection.
		public method Clear, void
		proc
			p_Collection.Clear()
		endmethod

		;Inserts an element into the collection at the specified index.
		public method Insert, void
			required in AtIndex, int
			required in RpsKeySegment, @RpsKeySegment
			endparams
		proc
			p_Collection.Insert(AtIndex,RpsKeySegment)
		endmethod

		;Searches for the specified object and returns the one-based index of
		;the last occurrence within the entire collection. 
		public method LastIndexOf, int
			required in RpsKeySegment, @RpsKeySegment
		proc
			mreturn p_Collection.LastIndexOf(RpsKeySegment)
		endmethod

		;Searches for the specified object and returns the one-based index of
		;the last occurrence within the range of elements in the collection that
		;extends from the first element to the specified index.
		public method LastIndexOf, int
			required in RpsKeySegment, @RpsKeySegment
			required in StartIndex, int
		proc
			mreturn p_Collection.LastIndexOf(RpsKeySegment,StartIndex)
		endmethod

		;Searches for the specified object and returns the one-based index of
		;the last occurrence within the range of elements in the collection that
		;contains the specified number of elements and ends at the specified index. 
		public method LastIndexOf, int
			required in RpsKeySegment, @RpsKeySegment
			required in StartIndex, int
			required in Count, int
		proc
			mreturn p_Collection.LastIndexOf(RpsKeySegment,StartIndex,Count)
		endmethod

	endclass

endnamespace
